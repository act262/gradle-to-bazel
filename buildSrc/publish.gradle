group plugin_group
version plugin_version

def artifactName = plugin_name

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

/* =============  publish to mavenLocal ============= */
publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId project.group
            artifactId artifactName
            version project.version
        }

    }
}

/* ==============  publish to bintray  =============== */
bintray {
    user = property('BINTRAY_USER')
    key = property('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = artifactName
        desc = 'android gradle project generate bazel build'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/act262/gradle-to-bazel'
        vcsUrl = 'https://github.com/act262/gradle-to-bazel'

        version {
            name = project.version
            desc = 'preview'
            released = new Date()
            attributes = ['gradle-plugin': 'android']
        }
    }

    publications = ['plugin']
    publish = true
    override = true
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    extension = 'jar'
    group 'publishing'
    description = 'Assembles a jar archive containing the main sources of this project.'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    group 'publishing'
    description = 'Assembles a jar archive containing the generated Javadoc API documentation of this project.'
    from groovydoc
}
